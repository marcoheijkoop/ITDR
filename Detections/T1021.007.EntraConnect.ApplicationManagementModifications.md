# T1021.007.EntraConnect.ApplicationManagementModifications

Microsoft Entra Connect Sync accounts in Entra ID have the 'Directory Synchronization Accounts' role assigned. This role grants sensitive privileges to modify settings of workload identities.
During regular operations, the sync accounts are not used to modify workload identities. Therefore, any modification of workload identities by the sync accounts should be investigated.

## Hunt Tags

**ID:** T1021.007.EntraConnect.ApplicationManagementModifications

**Author:** [Nicola Suter](https://nicolasuter.ch)

**License:** [MIT License](https://github.com/nicolonsky/ITDR/blob/main/LICENSE)

**References:** [Microsoft Entra built-in roles: Directory Synchronization Accounts](https://learn.microsoft.com/en-us/entra/identity/role-based-access-control/permissions-reference#directory-synchronization-accounts)

## ATT&CK Tags

**Tactic:** Lateral Movement (TA0008)

**Technique:** Remote Services: Cloud Services (T1021.007)

## Technical description of the attack

Attackers can gain access to the Microsoft Entra Connect Sync Account and use it to modify privileged workload identities as lateral movement path.

## Permission required to execute the technique

Possession of the Microsoft Entra Connect Sync Account credentials in Entra ID or access to the Entra Connect sync server.

## Detection description

Modifications in Entra ID in the category of _ApplicationManagement_ should not originate from the Microsoft Entra Connect Sync Account. This detection looks for modifications in this category that originate from the Microsoft Entra Connect Sync Account.

## Utilized Data Source

| Event ID | Event Name                    | Log Provider | ATT&CK Data Source |
| -------- | ----------------------------- | ------------ | ------------------ |
| -        | AuditLogs    | Entra ID     | Cloud Service      |
| -        | IdentityInfo<sup>1</sup> | Defender for Identity    | Cloud Service      |

1. The IdentityInfo source is optional and used as additional indicator whether an account is member of the 'Directory Synchronization Accounts' role.

## Hunt details

### KQL

**FP Rate:** _Low_

**Source:** _Entra ID_

**Description:** _This detection looks for Entra ID audit events in the category of 'ApplicationManagement' that originate from the Microsoft Entra Connect Sync Account._

**Query:**

```kusto
let lookback = 90d;
let DirSyncAccountRoleMembers = IdentityInfo 
    | where AssignedRoles has "Directory Synchronization Accounts" 
    | project AccountObjectId;
AuditLogs
| where TimeGenerated > ago(lookback)
| where Category =~ 'ApplicationManagement'
| extend Actor = iif(isnotempty(InitiatedBy.app), tostring(InitiatedBy.app.displayName), tostring(InitiatedBy.user.userPrincipalName))
| extend ActorId = iif(isnotempty(InitiatedBy.app), tostring(InitiatedBy.app.id), tostring(InitiatedBy.user.id))
// Enta Connect Sync Accounts start with the Sync_ prefix and have the Directory Synchronization Accounts Role assigned
| where Actor startswith "Sync_" or ActorId in~ (DirSyncAccountRoleMembers)
| project-away
    ResourceId,
    OperationName,
    ResultSignature,
    DurationMs,
    ResourceGroup,
    Resource,
    Level
```

## Considerations

- In case the sync account does not match the _Sync__ prefix, MDI IdentityInfo can be used to identify the account based on the Directory Synchronization Accounts role assignment.

## False Positives

False positives are unlikely.

## Detection Blind Spots

- This detection only covers malicious modifications of workload identities. It does not cover other malicious operations that can be performed by the sync account.

## References

- <https://learn.microsoft.com/en-us/entra/identity/role-based-access-control/permissions-reference#directory-synchronization-accounts>
- <https://github.com/Cloud-Architekt/AzureAD-Attack-Defense/blob/main/AADCSyncServiceAccount.md#attack-scenarios>
